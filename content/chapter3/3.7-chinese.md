# 3.7 检测Eigen库

**NOTE**:*此示例代码可以在 https://github.com/dev-cafe/cmake-cookbook/tree/v1.0/chapter-03/recipe-07 中找到，包含C++的示例。该示例在CMake 3.9版(或更高版本)中是有效的，并且已经在GNU/Linux、macOS和Windows上进行过测试。https://github.com/dev-cafe/cmake-cookbook/tree/v1.0/chapter-03/recipe-06 中也有一个适用于CMake 3.5的C++示例。*

BLAS库为矩阵和向量操作提供了标准化接口。不过，这个接口用Fortran语言书写。虽然已经展示了如何从C++直接使用这些库，但在现代C++程序中，更希望有高级的接口。

实现使用头文件的Eigen库，使用模板编程来提供接口。矩阵和向量类型易于使用，会在编译时提供类型检查，以确保没有不兼容的矩阵维度。密集和稀疏矩阵的运算，如：矩阵-矩阵乘积，线性系统求解器，和特征值问题，也可使用表达式模板高效的进行实现。从3.3版开始，Eigen可以链接到BLAS和LAPACK库，这可以将某些操作实现进行卸载，从而使库的实现更加灵活，从而获得更多的性能收益。

本示例将展示如何查找Eigen库，使用OpenMP并行化，并将部分工作转移到BLAS库。

矩阵-向量乘法和LU分解，会在本示例中实现，可以选择卸载BLAS和LAPACK库中的一些实现。这个示例中，只考虑将在BLAS库中卸载。

## 具体实施

这个示例中，我们将用到Eigen和BLAS库，以及OpenMP，并将Eigen使用OpenMP并行化，从BLAS库中卸载部分线性代数:

1. 我们首先声明最低CMake版本、项目名称和使能C++11语言标准:

   ```cmake
   cmake_minimum_required(VERSION 3.9 FATAL_ERROR)
   
   project(recipe-07 LANGUAGES CXX)
   
   set(CMAKE_CXX_STANDARD 11)
   set(CMAKE_CXX_EXTENSIONS OFF)
   set(CMAKE_CXX_STANDARD_REQUIRED ON)
   ```

2. 我们也要用到OpenMP，因为Eigen可以使用共享内存的方式，并行处理计算密集型操作:

   ```cmake
   find_package(OpenMP REQUIRED)
   ```

3. 调用`find_package`来搜索Eigen(将在下一节中讨论):

   ```cmake
   find_package(Eigen3 3.3 REQUIRED CONFIG)
   ```

4. 如果找到Eigen，我们将打印状态信息。注意，我们使用的是`Eigen3::Eigen`。正如前两个示例中了解到的，这是一个`IMPORT`目标，可通过提供的CMake脚本找到这个目标:

   ```cmake
   if(TARGET Eigen3::Eigen)
     message(STATUS "Eigen3 v${EIGEN3_VERSION_STRING} found in ${EIGEN3_INCLUDE_DIR}")
   endif()
   ```

5. 接下来，将源文件声明为可执行目标:

   ```cmake
   add_executable(linear-algebra linear-algebra.cpp)
   ```

6. 然后，找到BLAS。注意，现在不需要依赖项:

   ```cmake
   find_package(BLAS)
   ```

7. 如果找到BLAS，我们可为可执行目标，设置相应的编译定义和链接库:

   ```cmake
   if(BLAS_FOUND)
     message(STATUS "Eigen will use some subroutines from BLAS.")
     message(STATUS "See: http://eigen.tuxfamily.org/dox-devel/TopicUsingBlasLapack.html")
     target_compile_definitions(linear-algebra
       PRIVATE
       	EIGEN_USE_BLAS
       )
     target_link_libraries(linear-algebra
       PUBLIC
       	${BLAS_LIBRARIES}
       )
   else()
   	message(STATUS "BLAS not found. Using Eigen own functions")
   endif()
   ```

8. 最后，我们链接到`Eigen3::Eigen`和`OpenMP::OpenMP_CXX`目标。这就可以设置所有必要的编译标示和链接标志:

   ```cmake
   target_link_libraries(linear-algebra
     PUBLIC
       Eigen3::Eigen
       OpenMP::OpenMP_CXX
     )	
   ```

9. 现在已经准备好配置:

   ```shell
   $ mkdir -p build
   $ cd build
   $ cmake ..
   
   -- ...
   -- Found OpenMP_CXX: -fopenmp (found version "4.5")
   -- Found OpenMP: TRUE (found version "4.5")
   -- Eigen3 v3.3.4 found in /usr/include/eigen3
   -- ...
   -- Found BLAS: /usr/lib/libblas.so
   -- Eigen will use some subroutines from BLAS.
   -- See: http://eigen.tuxfamily.org/dox-devel/TopicUsingBlasLapack.html
   ```

10. 最后，编译并测试代码。注意，可执行文件使用四个线程:

    ```shell
    $ cmake --build .
    $ ./linear-algebra 1000
    
    Number of threads used by Eigen: 4
    matrices allocated and initialized Sun Jun 17 2018 11:04:20 AM
    elapsed time: 0.0492328s
    Scaling done, A and b saved Sun Jun 17 2018 11:04:20 AM
    elapsed time: 0.0492328s
    Linear system solver done Sun Jun 17 2018 11:04:20 AM
    elapsed time: 0.483142s
    relative error is 4.21946e-13
    ```

## 如何工作

Eigen支持CMake，这样设置C++项目就会变得很容易。从3.3版开始，Eigen提供了CMake模块，这些模块导出了相应的目标`Eigen3::Eigen`。

`find_package`命令可以用于配置选项。向CMake发出信号，表明包搜索将不会使用`FindEigen3.cmake`模块，而是通过 `Eigen3Config.cmake`，`Eigen3ConfigVersion.cmake`和`Eigen3Targets.cmake`提供Eigen3安装的标准位置，`<installation-prefix>/share/eigen3/cmake`。这种包定位模式被称为“Config”模式，比我们一直使用的`  Find<package>.cmake `方式更通用。有关“模块”模式和“配置”模式的更多信息，可参考官方文档 https://cmake.org/cmake/help/v3.5/command/find_package.html 。

虽然Eigen3、BLAS和OpenMP声明为` PUBLIC`依赖项，但`EIGEN_USE_BLAS`编译定义声明为`PRIVATE`。可以在单独的库目标中收集库依赖项，而不是直接链接可执行文件。使用`PUBLIC/PRIVATE`关键字，可以根据库目标的依赖关系调整相应标志和定义。

## 更多信息

CMake将在预定义的位置层次结构中查找配置模块。首先是`CMAKE_PREFIX_PATH`，`  <package>_DIR`是接下来的搜索路径。因此，如果Eigen3安装在非标准位置，可以使用这两个选项来告诉CMake在哪里查找它:

1. 通过将Eigen3的安装前缀传递给`CMAKE_PREFIX_PATH`:

   ```shell
   $ cmake -D CMAKE_PREFIX_PATH=<installation-prefix> ..
   ```

2. 通过传递配置文件的位置作为`Eigen3_DIR`:

   ```shell
   $ cmake -D Eigen3_DIR=<installation-prefix>/share/eigen3/cmake/
   ```

   