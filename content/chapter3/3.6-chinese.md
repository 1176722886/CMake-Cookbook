# 3.6 检测MPI的并行环境

**NOTE**:*此示例代码可以在 https://github.com/dev-cafe/cmake-cookbook/tree/v1.0/chapter-03/recipe-06 中找到，包含C++和C的示例。该示例在CMake 3.9版(或更高版本)中是有效的，并且已经在GNU/Linux、macOS和Windows上进行过测试。https://github.com/dev-cafe/cmake-cookbook/tree/v1.0/chapter-03/recipe-06 中也有一个适用于CMake 3.5的C示例。*

消息传递接口(Message Passing Interface, MPI)，可以作为OpenMP(共享内存并行方式)的补充，它也是分布式系统上并行程序的实际标准。尽管，最新的MPI实现也允许共享内存并行，但高性能计算中的一种典型方法就是，在计算节点上OpenMP与MPI结合使用。MPI标准的实施包括:

1. 运行时库
2. 头文件和Fortran 90模块
3. 编译器的包装器，用来调用编译器，使用额外的参数来构建MPI库，以处理目录和库。通常，包装器`mpic++/mpiCC/mpicxx `用于C++，`mpicc`用于C，`mpifort`用于Fortran。
4. 启动MPI：应该启动程序，以编译代码的并行执行。它的名称依赖于实现，可以使用这几个命令启动：`mpirun`、`mpiexec`或`orterun`。

本示例，将展示如何在系统上找到合适的MPI实现，从而编译一个简单的“Hello, World”MPI例程。

## 具体实施

在这个示例中，我们先查找MPI实现：库、头文件、编译器包装器和启动器。为此，我们将用到`FindMPI.cmake`标准cmake模块:

1. 首先，我们定义了最低CMake版本、项目名称、支持的语言和语言标准:

   ```cmake
   cmake_minimum_required(VERSION 3.9 FATAL_ERROR)
   
   project(recipe-06 LANGUAGES CXX)
   
   set(CMAKE_CXX_STANDARD 11)
   set(CMAKE_CXX_EXTENSIONS OFF)
   set(CMAKE_CXX_STANDARD_REQUIRED ON)
   ```

2. 然后，调用find_package来定位MPI实现:

   ```cmake
   find_package(MPI REQUIRED)
   ```

3. 与前面的配置类似，我们定义了可执行文件的的名称和相关源码，并链接到目标:

   ```cmake
   add_executable(hello-mpi hello-mpi.cpp)
   
   target_link_libraries(hello-mpi
     PUBLIC
    	  MPI::MPI_CXX
     )
   ```

4. 配置和构建可执行文件:

   ```shell
   $ mkdir -p build
   $ cd build
   $ cmake -D CMAKE_CXX_COMPILER=mpicxx ..
   
   -- ...
   -- Found MPI_CXX: /usr/lib/openmpi/libmpi_cxx.so (found version "3.1")
   -- Found MPI: TRUE (found version "3.1")
   -- ...
   
   $ cmake --build .
   ```

5. 为了并行执行这个程序，我们使用`mpirun`启动器(本例中，启动了两个任务):

   ```shell
   $ mpirun -np 2 ./hello-mpi
   
   Hello world from processor larry, rank 1 out of 2 processors
   Hello world from processor larry, rank 0 out of 2 processors
   ```

## 如何工作

请记住，编译器包装器是对MPI库的编译器封装。底层实现中，将会调用相同的编译器，并使用额外的参数(如成功构建并行程序所需的头文件包含路径和库)来扩充它。

编译和链接源文件时，包装器用了哪些标志？我们可以使用`--showme`选项来查看。要找出编译器的标志，我们可以这样使用:

```shell
$ mpicxx --showme:compile

-pthread
```

为了找出链接器标志，我们可以这样:

```shell
$ mpicxx --showme:link

-pthread -Wl,-rpath -Wl,/usr/lib/openmpi -Wl,--enable-new-dtags -L/usr/lib/openmpi -lmpi_cxx -lmpi
```

与之前的OpenMP配置类似，我们发现到MPI的链接非常简单，这要归功于FindMPI模块提供的目标:

正如在前面的配方中所讨论的，对于CMake版本低于3.9，需要更多的工作量:

```cmake
add_executable(hello-mpi hello-mpi.c)

target_compile_options(hello-mpi
  PUBLIC
  	${MPI_CXX_COMPILE_FLAGS}
  )
  
target_include_directories(hello-mpi
  PUBLIC
  	${MPI_CXX_INCLUDE_PATH}
  )
  
target_link_libraries(hello-mpi
  PUBLIC
  	${MPI_CXX_LIBRARIES}
  )
```

本示例中，我们讨论了C++项目，但其中参数和方法对于C或Fortran项目同样有效。

