# 1.3 构建和链接静态库和动态库

**NOTE**: *这个示例代码可以在 https://github.com/devcafe/cmake-cookbook/tree/v1.0/chap-01/recipe03 找到，其中有C++和Fortran示例。该配置在CMake 3.5版(或更高版本)测试有效的，并且已经在GNU/Linux、macOS和Windows上进行了测试。*

项目中会有单个源文件，构建的多个可执行文件的可能。项目中有多个源文件，通常分布在不同子目录中。这种实践有助于项目的源代码结构，而且支持模块化、代码重用和关注点分离。同时，这种分离还简化，并加速项目的重新编译。本示例中，我们将展示如何将源代码编译到库中，以及如何将其他目标链接这些库。

## 准备工作

让我们回看一下第一个例子。这里并不在为可执行文件提供单个源文件，我们现在将引入一个类，用来包装要打印到屏幕上的消息。更新一下的`hello-world.cpp`:

```c++
#include "Message.hpp"

#include <cstdlib>
#include <iostream>

int main() {
  Message say_hello("Hello, CMake World!");
  std::cout << say_hello << std::endl;
  Message say_goodbye("Goodbye, CMake World");
  std::cout << say_goodbye << std::endl;
  return EXIT_SUCCESS;
}
```

`Message`类包装了一个字符串，并提供重载过的`<<`操作，并且包括两个源码文件：`Message.hpp`头文件与`Message.cpp`源文件。`Message.hpp`中的接口包含以下内容：

```c++
#pragma once

#include <iosfwd>
#include <string>

class Message {
public:
  Message(const std::string &m) : message_(m) {}
  friend std::ostream &operator<<(std::ostream &os, Message &obj) {
    return obj.printObject(os);
}
private:
  std::string message_;
  std::ostream &printObject(std::ostream &os);
};
```

`Message.cpp`实现如下：

```c++
#include "Message.hpp"

#include <iostream>
#include <string>

std::ostream &Message::printObject(std::ostream &os) {
  os << "This is my very nice message: " << std::endl;
  os << message_;
  return os;
}
```

## 具体实施

这两个文件也必须编译，CMakeLists.txt必须相应地修改。在本例中，先把它们编译成一个库，而不是直接编译成可执行文件:

1. 创建一个新目标——静态库。库的名称和源码文件名相同，具体代码如下：

   ```cmake
   add_library(message
     STATIC
       Message.hpp
       Message.cpp
     )
   ```

2. 创建`hello-world`可执行文件的目标部分不需要修改：

   ```cmake
   add_executable(hello-world hello-world.cpp)
   ```

3. 最后，将目标库链接到可执行目标：

   ```cmake
   target_link_libraries(hello-world message)
   ```

4. 我们使用以前相同的命令，对项目进行配置和构建。这次，库将被编译，然后将库连接到`hello-world`可执行文件：

   ```shell
   $ mkdir -p build
   $ cd build
   $ cmake ..
   $ cmake --build .
   
   Scanning dependencies of target message
   [ 25%] Building CXX object CMakeFiles/message.dir/Message.cpp.o
   [ 50%] Linking CXX static library libmessage.a
   [ 50%] Built target message
   Scanning dependencies of target hello-world
   [ 75%] Building CXX object CMakeFiles/hello-world.dir/hello-world.cpp.o
   [100%] Linking CXX executable hello-world
   [100%] Built target hello-world
   
   $ ./hello-world
   
   This is my very nice message:
   Hello, CMake World!
   This is my very nice message:
   Goodbye, CMake World
   ```

## 如何工作

本节的例子引入了两个新命令：

* add_library(message STATIC Message.hpp Message.cpp)：生成必要的构建指令，将指定的源代码编译到库中。`add_library`的第一个参数是目标的名称。可以在整个`CMakeLists.txt`中使用相同的名称来引用库。生成的库的实际名称将由CMake通过在前面添加前缀`lib`和适当的扩展名作为后缀来形成。生成库是根据第二个参数(`STATIC`或`SHARED`)和操作系统确定的。
* target_link_libraries(hello-world message) : 将库链接到可执行文件。此命令还确保`hello-world`可执行文件可以正确地依赖于消息库。因此，我们确保在消息库链接到`hello-world`可执行文件之前，完成消息库的构建。

编译成功后，构建目录包含`libmessage.a`一个静态库(在GNU/Linux上)和`hello-world`可执行文件。
CMake接受其他值作为`add_library`的第二个参数的有效值，我们来看下本书会用到的值：

* STATIC用于创建静态库，即编译文件的打包存档，以便在链接其他目标时使用，例如：可执行文件。
* SHARED用于创建动态库，即可以动态链接，并在运行时加载的库。可以在`CMakeLists.txt`中使用`add_library(message SHARED
  Message.hpp Message.cpp) `从静态库切换到动态共享对象(DSO)。
* OBJECT可用于将给定给`add_library`的列表中的源编译到目标文件，不将它们归档到静态库中，也不能将它们链接到共享对象中。如果需要一次性创建静态库和动态库，那么使用对象库尤其有用。我们将在本示例中演示。
* MODULE又是DSO组。与`SHARED`库不同，它们不链接到项目中的任何其他目标，但是可以在稍后进行动态加载。该参数可以用于构建运行时插件。

CMake还能够生成特殊类型的库。这不会在构建系统中产生输出，但是对于组织目标之间的依赖关系和构建需求非常有用：

* IMPORTED，此类库目标表示位于项目外部的库。此类库的主要用途是，对上游包提供的项目的现有依赖项进行建模。因此，`IMPORTED`库将被视为不可变的。我们将在本书的其他部分演示使用`IMPORTED`库的示例。参见: https://cmake.org/cmake/help/latest/manual/cmakebuildsystem.7.html#imported-targets 
* INTERFACE，这种库与`IMPORTED`库类似，但是该类型库可变，没有位置信息。它的主要用于，为我们项目之外的目标建模使用。我们将在配方5中演示`INTERFACE`库的示例，在第11章“打包项目”中以Conda包的形式分发依赖项。参见: https://cmake.org/cmake/help/latest/manual/cmake-buildsystem.7.html#interface-libraries 
* ALIAS，顾名思义，这种库为项目中已存在的库目标定义别名。不能为`IMPORTED`库选择别名。参见: https://cmake.org/cmake/help/latest/manual/cmake-buildsystem.7.html#alias-libraries

本例中，我们使用`add_library`直接集合了源代码。在后面的章节中，我们将演示`target_sources`CMake命令用于收集源代码，特别是在第7章“结构化项目”。请参见Craig Scott的这篇精彩博文: https://crascit.com/2016/01/31/enhanced-source-file-handling-with-target_sources/ ，其中有对`target_sources`命令的具体使用。

## 更多信息

现在我们展示CMake中，对象库功能的使用。修改CMakeLists.txt，使用相同的源文件：

```cmake
cmake_minimum_required(VERSION 3.5 FATAL_ERROR)
project(recipe-03 LANGUAGES CXX)

add_library(message-objs
	OBJECT
		Message.hpp
		Message.cpp
	)
	
# this is only needed for older compilers
# but doesn't hurt either to have it
set_target_properties(message-objs
	PROPERTIES
		POSITION_INDEPENDENT_CODE 1
	)
	
add_library(message-shared
	SHARED
		$<TARGET_OBJECTS:message-objs>
	)
	
add_library(message-static
	STATIC
		$<TARGET_OBJECTS:message-objs>
	)
	
add_executable(hello-world hello-world.cpp)

target_link_libraries(hello-world message-static)
```

首先，`add_library`命令更改为`add_library(Message-objs OBJECT Message.hpp Message.cpp)`。此外，我们必须确保编译的目标文件，与生成位置无关。这是通过使用`set_target_properties`命令设置`message-objs`目标的相应属性来实现的。

**NOTE**: *可能在某些平台和/或使用较老的编译器上，需要显式地为目标设置POSITION_INDEPENDENT_CODE属性。*

现在可以使用这个对象库来获取静态库(称为`message-static`)和动态库(称为` message-shared `)。重要的是要注意引用对象库的生成器表达式语法:`$<TARGET_OBJECTS:message-objs> `。生成器表达式是CMake在生成时(即配置之后)构造，用于生成特定于配置的构建输出。参见: https://cmake.org/cmake/help/latest/manual/cmake-generator-expressions.7.html 。我们将在第5章后面的*配置时和构建时*操作中深入研究生成器表达式。最后，将`hello-world`可执行文件链接到消息库的静态版本。

是否可以让CMake生成同名的两个库？换句话说，它们都可以被称为message，而不是message-static和message-shared吗？我们需要修改这两个目标的属性：

```cmake
add_library(message-shared
  SHARED
    $<TARGET_OBJECTS:message-objs>
	)

set_target_properties(message-shared
	PROPERTIES
		OUTPUT_NAME "message"
	)
	
add_library(message-static
	STATIC
		$<TARGET_OBJECTS:message-objs>
	)
	
set_target_properties(message-static
	PROPERTIES
		OUTPUT_NAME "message"
	)
```

我们可以链接到DSO吗？这取决于操作系统和编译器：

1. 在GNU/Linux和macOS上，不管选择什么编译器，它都可以工作。
2. 在Windows上，不能与Visual Studio兼容，但可以与MinGW和MSYS2兼容。

这是为什么呢？生成好的DSO组需要程序员限制符号的可见性。这是在编译器的帮助下实现的，但不同的操作系统和编译器上，约定不同。CMake有一个强大的机制来处理这个问题，我们将在第10章*编写安装程序*中解释它如何工作。